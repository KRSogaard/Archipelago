buildscript {
    ext {
        springBootVersion = '2.2.1.RELEASE'
    }

    repositories {
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

allprojects {
    repositories {
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
        maven {
            //Local DynamoDB repository
            url "https://s3-us-west-2.amazonaws.com/dynamodb-local/release"
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'

    sourceCompatibility = 13
    targetCompatibility = 13

    // Ena ble preview
    tasks.test {
        jvmArgs("--enable-preview")
    }
    tasks.withType(JavaCompile) {
        options.compilerArgs += "--enable-preview"
    }
    tasks.withType(JavaExec) {
        jvmArgs += '--enable-preview'
    }

    dependencies {
        // Test framework
        testImplementation group: 'junit', name: 'junit', version: '4.12'
        testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.+'

        compile 'org.slf4j:slf4j-api:1.7.25'

        // Google Guava
        implementation 'com.google.guava:guava:28.0-jre'
        implementation 'org.apache.commons:commons-lang3:3.0'

        testImplementation 'commons-io:commons-io:2.6'

        // Lombok
        compile 'org.projectlombok:lombok:1.+'  // utilities for POJOs
        annotationProcessor 'org.projectlombok:lombok:1.+'
        testAnnotationProcessor 'org.projectlombok:lombok:1.+'
    }

    sourceSets {
        main.java.srcDirs = ['src']
        main.resources.srcDirs = ['resources']
        test.java.srcDirs = ['tst']
        test.resources.srcDirs = ['tst/resources']
    }
}

project(':PackageService') {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'application'

    mainClassName = 'build.archipelago.kauai.Application'

    dependencies {
        implementation project(':PackageService.Core')
        implementation project(':PackageService.Common')
        implementation project(':Archipelago.Common')

        implementation 'org.springframework.boot:spring-boot-starter-jersey'
        implementation 'org.springframework.boot:spring-boot-starter-web'

        implementation 'com.amazonaws:aws-java-sdk-s3:1.11.692'
        implementation 'com.amazonaws:aws-java-sdk-dynamodb:1.11.692'

        testImplementation 'com.google.code.gson:gson:2.8.6'
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
    }

    task runlocal {
        run.configure {
            project.logger.lifecycle("======================================")
            project.logger.lifecycle("= Running application in alpha stage =")
            project.logger.lifecycle("======================================")
            systemProperty "spring.profiles.active", 'alpha'
        }
    }
    runlocal.finalizedBy run
}
project(':PackageService.Core') {
    dependencies {
        implementation project(':Archipelago.Common')
        implementation project(':PackageService.Common')

        implementation 'com.amazonaws:aws-java-sdk-s3:1.11.692'
        implementation 'com.amazonaws:aws-java-sdk-dynamodb:1.11.692'
    }
}
project(':PackageService.Client') {
    dependencies {
        implementation project(':Archipelago.Common')
        implementation project(':PackageService.Common')
    }
}
project(':PackageService.Common') {
    dependencies {
        implementation project(':Archipelago.Common')
    }
}

project(':VersionSetService') {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'application'

    mainClassName = 'build.archipelago.versionsetservice.Application'

    dependencies {
        implementation project(':VersionSetService.Core')
        implementation project(':Archipelago.Common')
        implementation project(':PackageService.Common')
        implementation project(':PackageService.Client')
        implementation project(':PackageService.Common')

        implementation 'org.springframework.boot:spring-boot-starter-jersey'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'com.amazonaws:aws-java-sdk-dynamodb:1.11.692'

        testImplementation 'com.google.code.gson:gson:2.8.6'
        testImplementation 'net.dongliu:gson-java8-datatype:1.1.0'
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }

        // Needed for DynamoDBLocal
        testCompile 'com.amazonaws:DynamoDBLocal:1.12.0'
        testCompile 'com.almworks.sqlite4java:sqlite4java:1.0.392'
    }

    // This allows for the inMemory DynamoDb
    task copyNativeDeps(type: Copy) {
        project.logger.lifecycle("Copying native libs")
        mkdir "build/native-libs"
        from(configurations.testCompile) {
            include '*.dll'
            include '*.dylib'
            include '*.so'
        }
        into 'build/native-libs'
    }
    test.dependsOn copyNativeDeps
    test.doFirst {
        project.logger.lifecycle("Setting java lib path to \"build/native-libs\"")
        systemProperty "java.library.path", 'build/native-libs'
    }

    task runlocal {
        run.configure {
            project.logger.lifecycle("======================================")
            project.logger.lifecycle("= Running application in alpha stage =")
            project.logger.lifecycle("======================================")
            systemProperty "spring.profiles.active", 'alpha'
        }
    }
    runlocal.finalizedBy run
}
project(':VersionSetService.Core') {
    dependencies {
        implementation project(':Archipelago.Common')
        implementation project(':Archipelago.Common.DynamoDB')
        implementation project(':PackageService.Client')
        implementation project(':PackageService.Common')
        implementation 'com.amazonaws:aws-java-sdk-dynamodb:1.11.692'

        // Needed for DynamoDBLocal
        testCompile 'com.amazonaws:DynamoDBLocal:1.12.0'
        testCompile 'com.almworks.sqlite4java:sqlite4java:1.0.392'
    }

    // This allows for the inMemory DynamoDb
    task copyNativeDeps(type: Copy) {
        project.logger.lifecycle("Copying native libs")
        mkdir "build/native-libs"
        from(configurations.testCompile) {
            include '*.dll'
            include '*.dylib'
            include '*.so'
        }
        into 'build/native-libs'
    }
    test.dependsOn copyNativeDeps
    test.doFirst {
        project.logger.lifecycle("Setting java lib path to \"build/native-libs\"")
        systemProperty "java.library.path", 'build/native-libs'
    }
}

project(':Maui') {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'application'

    mainClassName = 'build.archipelago.maui.Application'

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter'
        compile 'info.picocli:picocli:4.1.4'
        annotationProcessor 'info.picocli:picocli-codegen:4.1.4'
    }

    compileJava {
        options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
    }
}
project(':Maui.Core') {
    dependencies {
        implementation project(':Archipelago.Common')
        implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.3.0'
        implementation 'com.fasterxml.jackson.core:jackson-databind:2.2.3'
    }
}
project(':Archipelago.Common') {
    dependencies {
    }
}
project(':Archipelago.Common.DynamoDB') {
    dependencies {
        implementation project(':Archipelago.Common')
        implementation 'com.amazonaws:aws-java-sdk-dynamodb:1.11.692'
    }
}